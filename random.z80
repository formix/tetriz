
#if !defined(RANDOM_SEED)

#data _HEAP

RANDOM_SEED         DEFS 2


    ; MACRO RANDOMIZE
    ; LD      HL,(RANDOM_SEED)
    ; INC     HL
    ; LD      (RANDOM_SEED),HL
    ; ENDM


#code _PROGRAM_FOOTER


; 16-bit xorshift pseudorandom number generator by John Metcalf
; 20 bytes, 86 cycles (excluding ret)
; returns   hl = pseudorandom number
; corrupts   a
; generates 16-bit pseudorandom numbers with a period of 65535
; using the xorshift method:
; hl ^= hl << 7
; hl ^= hl >> 9
; hl ^= hl << 8
; some alternative shift triplets which also perform well are:
; 6, 7, 13; 7, 9, 13; 9, 7, 13.
XRND:
  ld hl,(RANDOM_SEED)
  ld a,h
  rra
  ld a,l
  rra
  xor h
  ld h,a
  ld a,l
  rra
  ld a,h
  rra
  xor l
  ld l,a
  xor h
  ld h,a
  ld (RANDOM_SEED),hl
  ret

#endif